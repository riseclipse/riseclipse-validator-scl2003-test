/*
*************************************************************************
**  Copyright (c) 2016-2022 CentraleSupélec & EDF.
**  All rights reserved. This program and the accompanying materials
**  are made available under the terms of the Eclipse Public License v2.0
**  which accompanies this distribution, and is available at
**  https://www.eclipse.org/legal/epl-v20.html
** 
**  This file is part of the RiseClipse tool
**  
**  Contributors:
**      Computer Science Department, CentraleSupélec
**      EDF R&D
**  Contacts:
**      dominique.marcadet@centralesupelec.fr
**      aurelie.dehouck-neveu@edf.fr
**  Web site:
**      https://riseclipse.github.io/
*************************************************************************
*/

import scl: 'http://www.iec.ch/61850/2003/SCL'

package scl

context ConnectedAP

    -- The iedName attribute shall refer to an existing IED.
    inv ConnectedAP_ied_exists
        (   'ERROR;'
          + 'OCL/SemanticConstraints/ConnectedAP_ied_exists;'
          + self.filename + ';'
          + self.lineNumber.toString() + ';'
          + 'iedName attribute shall refer to an existing IED in ConnectedAP. ' + 'Current value is ' + self.iedName.toString()
        )
    :
        let scl : SCL = self.ParentSubNetwork.ParentCommunication.ParentSCL in
        self.iedName <> null implies scl.IED->exists( i : IED | i.name = self.iedName )
        
    -- The ConnectedAP in SubNetwork shall have unique GSE cbName and ldInst (from SCL_Communication.xsd)
    inv ConnectedAP_unique_attributes_combination_GSE_from_SubNetwork
        (   'ERROR;'
          + 'OCL/SemanticConstraints/ConnectedAP_unique_attributes_combination_GSE_from_SubNetworkk;'
          + self.filename + ';'
          + self.lineNumber.toString() + ';'
          + 'Unique GSE cbName and ldInst combination in ConnectedAP from SubNetwork'
        )
    :
        self.oclContainer.oclIsTypeOf(SubNetwork) implies self.GSE -> isUnique( g : GSE | Sequence{ g.cbName , g.ldInst } )

    -- The ConnectedAP in SubNetwork shall have unique SMV cbName and ldInst (from SCL_Communication.xsd)
    inv ConnectedAP_unique_attributes_combination_SMV_from_SubNetwork
        (   'ERROR;'
          + 'OCL/SemanticConstraints/ConnectedAP_unique_attributes_combination_SMV_from_SubNetwork;'
          + self.filename + ';'
          + self.lineNumber.toString() + ';'
          + 'Unique SMV cbName and ldInst combination in ConnectedAP from SubNetwork'
        )
    :
        self.oclContainer.oclIsTypeOf(SubNetwork) implies self.SMV -> isUnique( s : SMV | Sequence{ s.cbName , s.ldInst } )
    
    -- a ConnectedAP must refer an existing AccessPoint
    inv ConnectedAP_RefersToAccessPoint
        (   'ERROR;'
          + 'OCL/SemanticConstraints/ConnectedAP_RefersToAccessPoint;'
          + self.filename + ';'
          + self.lineNumber.toString() + ';'
          + 'ConnectedAP (apName=' + self.apName.toString() + ')  does not refer an existing AccessPoint in IED section'
        )
    :
        self.RefersToAccessPoint <> null
endpackage

