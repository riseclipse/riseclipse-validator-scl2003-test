/*
*************************************************************************
**  Copyright (c) 2016-2022 CentraleSupélec & EDF.
**  All rights reserved. This program and the accompanying materials
**  are made available under the terms of the Eclipse Public License v2.0
**  which accompanies this distribution, and is available at
**  https://www.eclipse.org/legal/epl-v20.html
** 
**  This file is part of the RiseClipse tool
**  
**  Contributors:
**      Computer Science Department, CentraleSupélec
**      EDF R&D
**  Contacts:
**      dominique.marcadet@centralesupelec.fr
**      aurelie.dehouck-neveu@edf.fr
**  Web site:
**      https://riseclipse.github.io/
*************************************************************************
*/

import scl: 'http://www.iec.ch/61850/2003/SCL'

import '../Helpers/BaseSimpleTypes.ocl'

package scl

context ReportControl
-- extends ControlWithTriggerOpt

    -- The confRev attribute shall be present
    inv ReportControl_confRev_required
        (   'ERROR;'
          + 'OCL/RequiredAttributes/ReportControl_confRev_required;'
          + self.lineNumber.toString() + ';'
          + 'confRev attribute shall be present in ReportControl'
        )
    :
        self.confRev <> null

    -- The confRev attribute is an unsigned int
    inv ReportControl_confRev_unsigned_int
        (   'ERROR;'
          + 'OCL/RequiredAttributes/ReportControl_confRev_unsigned_int;'
          + self.lineNumber.toString() + ';'
          + 'confRev attribute shall be an unsigned int in ReportControl. ' + 'Current value is ' + self.confRev.toString()
        )
    :
        self.confRev <> null implies self.confRev >= 0

    --  The rptID can be missing, if the NULL value according to IEC 61850-7-2 is
    --  used. However, if the attribute is used, its value shall not be the empty
    --  string (however the empty string shall be accepted as input for backward
    --  compatibility).
    inv ReportControl_rptID_valid
        (   'ERROR;'
          + 'OCL/RequiredAttributes/ReportControl_rptID_valid;'
          + self.lineNumber.toString() + ';'
          + 'rptID attribute shall be valid in ReportControl. ' + 'Current value is ' + self.rptID.toString()
        )
    :
        self.rptID <> null implies self.validSclMessageID( rptID )

    -- buffered is a boolean
    
    -- bufTime is an unsigned int
    inv ReportControl_bufTime_valid
        (   'ERROR;'
          + 'OCL/RequiredAttributes/ReportControl_bufTime_valid;'
          + self.lineNumber.toString() + ';'
          + 'bufTime attribute shall be valid in ReportControl. ' + 'Current value is ' + self.bufTime.toString()
        )
    :
        self.bufTime <> null implies self.bufTime >= 0

    -- indexed is a boolean
    

endpackage

